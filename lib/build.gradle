apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'

version = "${rootProject.versionMajor}.${rootProject.versionMinor}.${rootProject.versionPatch}"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionName rootProject.versionName
        versionCode rootProject.versionCode
//        setProperty("archivesBaseName", "taolog-$version")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2-native-mt'

    def archLifecycleVersion = '2.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"
    implementation 'com.google.android.material:material:1.2.1'
}

dokkaHtml {
    outputDirectory =  "$buildDir/dokka"
    dokkaSourceSets {
        create ( "main" ) {
            noAndroidSdkLink =  true
        }
        create ( "jvmMain" ) {}
    }
}

// Publishing

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def gitUser = "lordtao"
def gitProject = "android-tao-log"
def siteUrl = 'https://github.com/${gitUser}/${gitProject}'
def gitUrl = 'https://github.com/${gitUser}/${gitProject}.git'

// Hide properties from ext users
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = gitProject
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = [LICENSE]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name LIB_DESCRIPTION
                url siteUrl
                licenses {
                    license {
                        name LICENSE
                        url LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id gitUser
                        name 'Alexandr Tsvetkov'
                        email 'tsvetkov2010@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions  {
    doLast {
        println project.getConvention()
    }
}
